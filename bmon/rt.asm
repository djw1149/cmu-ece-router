	SECTION 9
*
*	68000 SUPPORT LIBRARY
*	COPYRIGHT 1984 GREEN HILLS SOFTWARE
*	MAY 28, 1984
*	32 REMAINDER,DIVIDE, AND MULTIPLY PACKAGE
*
*	THIS PACKAGE IS DIVIDED INTO TWO PARTS.
*	ALL REMAINDERS AND DIVIDES ARE DONE WITH ENTRY
*	POINTS TO A 32 BIT DIVIDE/REMAINDER ROUTINE.
*	THIS PACKAGE USES A0,A1,D0,D1,D2.
*
*	UNSIGNED AND SIGNED 32 BIT MULTIPLIES ARE IDENTICAL,
*	AND ARE PERFORMED BY THE SAME ROUTINE. THIS ROUTINE
*	USES D0,D1,D2, AND A0.
*
*	32 BIT REMAINDER AND DIVIDE ROUTINE
*	PARAMETERS IN D1 AND D2
*	THIS PACKAGE HAS BEEN TESTED ON 5000 RANDOM NUMBER
*	PAIRS AGAINST THE VAX. THE NUMBER GENERATION SCHEME
*	GENERATED NUMBERS OF SIZE 1-32 BITS RANDOMLY, AND SHOULD
*	THEREFORE HAVE EXERCISED MOST CODE PATHS.
*
*	COMMENTS AND SUGGESTIONS FOR IMPROVEMENTS ARE WELCOME.
POSDIV:	CMP.L	#$FFFF,D2	*DIVISOR MORE THAN 16 BITS OF SIGNIF?
	BHI.S	LONGDIV		*YES, GOTO ITERATION DIVIDE
	MOVE.W	D1,A1		*STANDARD 32/16 DIVIDE WITHOUT LOSS
	CLR.W	D1
	SWAP	D1
	DIVU.W	D2,D1
	MOVE.L	D1,D0
	SWAP	D1
	MOVE.W	A1,D0
	DIVU.W	D2,D0
	MOVE.W	D0,D1
	CLR.W	D0
	SWAP	D0
	RTS
LONGDIV:	MOVE.L	D1,D0
	CLR.W	D0
	SWAP	D0
	SWAP	D1
	CLR.W	D1
	MOVE.L	D2,A1
	MOVEQ.L	#$F,D2
LABEL1:	ADD.L	D1,D1		*BEGIN DIVIDE BY ITERATION
	ADDX.L	D0,D0
	CMP.L	D0,A1
	BHI.S	LDEX
	SUB.L	A1,D0
	ADDQ.W	#1,D1
LDEX:	DBF	D2,LABEL1
	RTS
*
* lmul, ulmul
* 32 BIT MULTIPLY ROUTINES
* 
	XDEF	lmult
	XDEF	ulmult
lmult:
ulmult:
	MOVE.L	D2,-(SP)	*SAVE D2 REGISTER
	MOVE.L	D0,D2
	MULU.W	D1,D2
	MOVE.L	D2,A0
	MOVE.L	D0,D2
	SWAP	D2
	MULU.W	D1,D2
	SWAP	D1
	MULU.W	D1,D0
	ADD.L	D2,D0
	SWAP	D0
	CLR.W	D0
	ADD.L	D0,A0
	MOVE.L	A0,D0
	MOVE.L	(SP)+,D2
	RTS
*
*	ulmodt
*	UNSIGNED 32 BIT REMAINDER 
	XDEF	ulmodt
ulmodt:	MOVE.L	D2,-(SP)		*SAVE D2
	MOVE.L	D1,D2	*SETUP PARAMETERS
	MOVE.L	D0,D1
	BSR.S	POSDIV		*CALL UNSIGNED DIV/REM ROUTINE
	MOVE.L	(SP)+,D2		*RESTORE D2
	RTS
*
*	uldiv
*	UNSIGNED 32 BIT DIVIDE
	XDEF	uldivt
uldivt:
	MOVE.L	D2,-(SP)		*SAVE D2 AND D3
	MOVE.L	D1,D2
	MOVE.L	D0,D1	*LOAD UP PARAMETERS
	BSR.S	POSDIV
	MOVE.L	D1,D0
	MOVE.L	(SP)+,D2
	RTS
*
*	lrem
*	SIGNED 32 BIT REMAINDER
*	IF DIVISOR IS NEGATIVE, RESULT IS NEGATIVE.
	XDEF	lmodt
lmodt:
	MOVE.L	D2,-(SP)	*SAVE D2
	MOVE.L	D1,D2
	BGE.S	lrem1
	NEG.L	D2
lrem1:
	MOVE.L	D0,D1
	MOVEQ.L	#0,D0		*NEGATION FLAG OFF
	TST.L	D1
	BGE.S	lrem2
	NEG.L	D1
	NOT.L	D0		*NEGATION FLAG ON
lrem2:	MOVE.L	D0,A0		*MOVE FLAG TO A SAFE PLACE
	BSR.L	POSDIV
	MOVE.W	A0,D2
	BEQ.S	lremDONE
	NEG.L	D0
lremDONE:	MOVE.L (SP)+,D2		*RESTORE D2 
	RTS
*
*	ldivt	
*	SIGNED 32 BIT DIVIDE
*	IF DIVIDEND XOR DIVISOR IS NEGATIVE,
*	RESULT MUST BE NEGATIVE.
	XDEF	ldivt
ldivt:
	MOVE.L	D2,-(SP)
	MOVE.L	D0,A0
	MOVEQ.L	#0,D0
	MOVE.L	D1,D2
	BGE.S	ldiv1
	NEG.L	D2		*NEGATE OPERAND
	NOT.L	D0
ldiv1:	MOVE.L	A0,D1
	BGE.S	ldiv2
	NEG.L	D1		*NEGATE OPERAND
	NOT.L	D0
ldiv2:	MOVE.L	D0,A0		*TUCK AWAY NEGATION FLAG
	BSR.L	POSDIV
	MOVE.L	A0,D2
	BEQ.S	ldivRET
	NEG.L	D1
ldivRET:	MOVE.L	D1,D0
	MOVE.L	(SP)+,D2
	RTS
	END
